plugins {
    id 'java-library'
    id 'application'
}

mainClassName = 'pl.allegro.tech.hermes.management.HermesManagement'

dependencies {
    api project(':hermes-api')
    api project(':hermes-common')
    api project(':hermes-tracker')
    implementation project(':hermes-schema')

    api group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.spring
    api group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.spring
    api group: 'org.springframework.boot', name: 'spring-boot-starter-jersey', version: versions.spring
    implementation group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '4.8'
    implementation group: 'org.glassfish.jersey.ext', name: 'jersey-mvc-freemarker', version: versions.jersey

    implementation (group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: '1.6.3') {
        exclude group: 'javax.validation', module: 'validation-api'
    }

    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: versions.kafka

    implementation group: 'commons-codec', name: 'commons-codec', version: '1.9'
    implementation (group: 'com.github.fge', name: 'json-schema-validator', version: '2.2.6') {
        exclude group: 'net.sf.jopt-simple'
    }

    implementation group: 'commons-jxpath', name: 'commons-jxpath', version: '1.3'
    implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'

    api group: 'org.javers', name: 'javers-core', version: '5.2.5'

    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    testImplementation project(':hermes-test-helper')
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.spring

    testImplementation group: 'org.spockframework', name: 'spock-core', version: versions.spock
    testImplementation group: 'org.spockframework', name: 'spock-junit4', version: versions.spock
    testImplementation group: 'org.spockframework', name: 'spock-spring', version: versions.spock
    testImplementation group: 'org.apache.groovy', name: 'groovy-json', version: versions.groovy

    testImplementation group: 'org.testcontainers', name: 'spock', version: versions.testcontainers
    testImplementation group: 'org.testcontainers', name: 'kafka', version: versions.testcontainers
}

task buildHermesConsole(type: Exec) {
    workingDir '../hermes-console'
    commandLine './package.sh'
}

task attachHermesConsole(type: Copy, dependsOn: 'buildHermesConsole') {
    from '../hermes-console/dist/static'
    into(sourceSets.main.output.resourcesDir.path + '/static')
}

tasks.register('prepareIndexTemplate') {
    doLast {
        def indexPath = sourceSets.main.output.resourcesDir.path + '/static/index.html'
        ant.move file: indexPath,
                tofile: indexPath + '.ftl'
    }
}

jar.dependsOn(attachHermesConsole, 'prepareIndexTemplate')
