
repositories {
    maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
}

project.configurations {
    integration {
        extendsFrom project.configurations.testRuntime
        description = 'Dependencies for integration tests'
        transitive = true
        visible = true
    }
    alpnboot
}


/*
    This is required for proper http2 integration testing. We only need one version at a time but it's rather
    impossible to know which update of Java8 developers are running so we're trying to support all common use cases;
    code refers to: http://www.eclipse.org/jetty/documentation/current/alpn-chapter.html#alpn-versions
 */
def getAlpnVersion() {
    def matcher = (System.getProperty("java.version") =~ /_(\d+)-/)
    def version = matcher.find() ? matcher.group(1) : null
    switch(version) {
        case 0..24:
            return '8.1.0.v20141016'
        case 25..30:
            return '8.1.2.v20141202'
        default:
            return '8.1.3.v20150130'
    }
}

dependencies {
    compile project(':hermes-common')
    compile project(':hermes-consumers')
    compile project(':hermes-frontend')
    compile project(':hermes-management')
    compile project(':hermes-client')
    compile project(':hermes-tracker')
    compile project(':hermes-tracker-mongo')

    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.6.v20151106'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.6.v20151106'

    testCompile project(':hermes-test-helper')
    testCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    testCompile group: 'org.apache.curator', name: 'curator-test', version: versions.curator
    testCompile group: 'com.github.fakemongo', name: 'fongo', version: versions.fongo
    testCompile group: 'org.springframework', name: 'spring-web', version: '4.1.4.RELEASE'
    testCompile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.4.0'

    testCompile (group: 'org.schemarepo', name: 'schema-repo-server', version: '0.1.3') {
        exclude module: 'jetty-server'
        exclude module: 'jetty-servlet'
    }

    testCompile(group: 'org.hornetq', name: 'hornetq-jms-server', version: '2.4.1.Final') {
        exclude module: 'hornetq-native'
    }

    testCompile group: 'org.eclipse.jetty.alpn', name: 'alpn-api', version: versions.alpn_api
    alpnboot group: 'org.mortbay.jetty.alpn', name: 'alpn-boot', version: getAlpnVersion()
}

project.sourceSets {
    integration {
        java.srcDir project.file('src/integration/java')

        resources.srcDir project.file('src/integration/resources')
        resources.srcDir project.sourceSets.test.resources
        resources.srcDir project.sourceSets.main.resources

        project.plugins.withType(GroovyPlugin) {
            groovy.srcDir project.file('src/integration/groovy')
        }

        compileClasspath = project.sourceSets.main.output +
                project.configurations.testRuntime +
                project.configurations.integration
        runtimeClasspath = output + compileClasspath + project.configurations.testRuntime
    }
}

task integrationTest(type: Test) {
    def args = ["-Xbootclasspath/p:${project.configurations.alpnboot.asPath}"]
    if(project.hasProperty('tests.timeout.multiplier')) {
        args += "-Dtests.timeout.multiplier=${project.property('tests.timeout.multiplier')}"
    }

    jvmArgs = args
    maxHeapSize "2g"

    group = 'Verification'
    description = 'Runs the integration tests.'

    testClassesDir = project.sourceSets.integration.output.classesDir
    classpath = project.sourceSets.integration.runtimeClasspath

    useTestNG()

    testLogging {
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardError"
    }
}

