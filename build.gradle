buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath group: 'io.codearte.gradle.nexus', name: 'gradle-nexus-staging-plugin', version: '0.5.1'
        classpath group: 'com.bmuschko', name: 'gradle-nexus-plugin', version: '2.3.1'
        classpath group: 'gradle.plugin.me.champeau.gradle', name: 'jmh-gradle-plugin', version: '0.3.0'
    }
}

plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.4.0'
}

apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = 'hermes'
    }

    versionCreator 'versionWithBranch'

    hooks {
        pre 'fileUpdate', [files: ['mkdocs.yml', 'hermes-console/package.json'], pattern: {v, p -> /$v/ }, replacement: {v, p -> "$v"}]
        pre 'commit'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    group = 'pl.allegro.tech.hermes'
    version = scmVersion.version

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    project.ext.versions = [
            guava       : '20.0',
            jackson     : '2.8.3',
            jersey      : '2.23.2',
            jetty       : '9.3.18.v20170406',
            curator     : '2.11.1',
            wiremock    : '1.58',
            fongo       : '1.6.1',
            spock       : '1.0-groovy-2.4',
            alpn_api    : '1.1.2.v20150522'
    ]

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.0.2'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.1.0'
        testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.6.1'
        testCompile group: 'com.googlecode.catch-exception', name: 'catch-exception', version: '1.2.0'
    }

    test {
        if(project.hasProperty('tests.timeout.multiplier')) {
            jvmArgs = ["-Dtests.timeout.multiplier=${project.property('tests.timeout.multiplier')}"]
        }
    }
}

configure(subprojects - project(':integration')) {
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'jacoco'

    modifyPom {
        project {
            name project.name
            description 'Fast and reliable message broker built on top of Kafka.'
            url 'https://github.com/allegro/hermes'
            inceptionYear '2015'

            scm {
                url 'https://github.com/allegro/hermes'
                connection 'scm:git@github.com:allegro/hermes.git'
                developerConnection 'scm:git@github.com:allegro/hermes.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'skylab'
                    name 'Team Skylab'
                }
            }
        }
    }
}

subprojects {
    configurations {
        all*.exclude group: "org.slf4j", module: "slf4j-log4j12"
        all*.exclude group: "log4j", module: "log4j"
        all*.resolutionStrategy*.force 'org.jboss.logging:jboss-logging:3.2.1.Final'
        all*.resolutionStrategy*.force "com.google.guava:guava:${versions.guava}"
        all*.resolutionStrategy*.force "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
        all*.resolutionStrategy*.force "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}"
        all*.resolutionStrategy*.force "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${versions.jackson}"
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked,deprecation"
    }

    test {
        testLogging {
            exceptionFormat = 'full'
            events 'passed', 'skipped', 'failed'
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    project.configurations {
        provided {
            description = 'Provided dependencies'
            transitive = true
        }
    }

    project.sourceSets {
        main {
            compileClasspath += project.configurations.provided
        }
        test {
            compileClasspath += project.configurations.provided
            runtimeClasspath += project.configurations.provided
        }
    }
}

nexusStaging {
    packageGroup = "pl.allegro"

    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
