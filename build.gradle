buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath group: 'io.codearte.gradle.nexus', name: 'gradle-nexus-staging-plugin', version: '0.5.1'
        classpath group:  'com.bmuschko', name: 'gradle-nexus-plugin', version: '2.3.1'
    }
}

plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.3.0'
}

apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = 'hermes'
    }

    versionCreator 'versionWithBranch'

    branchVersionCreators = [
            'rc': { version, position -> version }
    ]

    hooks {
        pre 'fileUpdate', [files: ['mkdocs.yml', 'hermes-console/package.json'], pattern: {v, p -> /$v/ }, replacement: {v, p -> "$v"}]
        pre 'commit'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'pl.allegro.tech.hermes'
    version = scmVersion.version

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    project.ext.versions = [
            guava       : '16.0.1',
            jackson     : '2.5.1',
            jersey      : '2.12',
            curator     : '2.7.0',
            wiremock    : '1.58',
            fongo       : '1.6.1',
            spock       : '1.0-groovy-2.4',
            alpn_api    : '1.1.2.v20150522'
    ]

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.0.2'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.1.0'
        testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.6.1'
        testCompile group: 'com.googlecode.catch-exception', name: 'catch-exception', version: '1.2.0'
    }
}

configure(subprojects - project(':integration')) {
    apply plugin: 'com.bmuschko.nexus'

    modifyPom {
        project {
            name project.name
            description 'Fast and reliable message broker built on top of Kafka.'
            url 'https://github.com/allegro/hermes'
            inceptionYear '2015'

            scm {
                url 'https://github.com/allegro/hermes'
                connection 'scm:git@github.com:allegro/hermes.git'
                developerConnection 'scm:git@github.com:allegro/hermes.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'skylab'
                    name 'Team Skylab'
                }
            }
        }
    }
}

subprojects {
    configurations {
        all*.exclude group: "org.slf4j", module: "slf4j-log4j12"
        all*.exclude group: "log4j", module: "log4j"
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked,deprecation"
    }

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    project.configurations {
        provided {
            description = 'Provided dependencies'
            transitive = true
        }
    }

    project.sourceSets {
        main {
            compileClasspath += project.configurations.provided
        }
        test {
            compileClasspath += project.configurations.provided
            runtimeClasspath += project.configurations.provided
        }
    }


}

nexusStaging {
    packageGroup = "pl.allegro"

    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
