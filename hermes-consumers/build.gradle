configurations {
    sbeClasspath
}

dependencies {
    compile project(':hermes-common')
    compile project(':hermes-tracker')
    compile project(':hermes-metrics')
    compile project(':hermes-schema')

    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        compile group: 'org.eclipse.jetty', name: 'jetty-client', version: versions.jetty
        compile group: 'org.eclipse.jetty', name: 'jetty-alpn-openjdk8-client', version: versions.jetty
    } else {
        compile group: 'org.eclipse.jetty', name: 'jetty-alpn-java-client', version: versions.jetty
    }
    compile group: 'org.eclipse.jetty.http2', name: 'http2-http-client-transport', version: versions.jetty

    compile group: 'org.jctools', name: 'jctools-core', version: '1.2'

    compile group: 'javax.jms', name: 'javax.jms-api', version: '2.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.5'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.1'
    compile(group: 'com.github.rholder', name: 'guava-retrying', version: '2.0.0') {
        exclude module: 'guava'
    }

    compile group: 'org.agrona', name: 'agrona', version: '1.0.6'

    compile(group: 'org.hornetq', name: 'hornetq-jms-client', version: '2.4.1.Final') {
        exclude module: 'hornetq-native'
    }

    testCompile project(':hermes-test-helper')
    testCompile group: 'org.apache.curator', name: 'curator-test', version: versions.curator
    testCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    testCompile project(':hermes-common')

    testCompile(group: 'com.jayway.awaitility', name: 'awaitility', version: '1.6.1') {
        exclude group: 'com.jayway.jsonpath', module: 'json-path'
    }

    testCompile group: 'tech.allegro.schema.json2avro', name: 'converter', version: versions.json2avro

    testCompile group: 'org.spockframework', name: 'spock-core', version: versions.spock

    sbeClasspath group: 'uk.co.real-logic', name: 'sbe-all', version: '1.12.8'
}

if (project.hasProperty('distribution')) {
    apply plugin: 'application'
    mainClassName = 'pl.allegro.tech.hermes.consumers.HermesConsumers'
}

def generatedPath = "${buildDir}/generated/java/"

task generateMaxRateSbeStubs(type: JavaExec) {
    description = "Generate SBE stubs for max-rate"
    classpath configurations.sbeClasspath
    main = 'uk.co.real_logic.sbe.SbeTool'
    systemProperties(
            'sbe.output.dir': generatedPath,
            'sbe.xinclude.aware': 'true'
    )
    args = ['src/main/resources/sbe/max-rate.xml']
}

task generateWorkloadSbeStubs(type: JavaExec) {
    description = "Generate SBE stubs for workload"
    classpath configurations.sbeClasspath
    main = 'uk.co.real_logic.sbe.SbeTool'
    systemProperties(
            'sbe.output.dir': generatedPath,
            'sbe.xinclude.aware': 'true'
    )
    args = ['src/main/resources/sbe/workload.xml']
}

task generateSbeStubs(type: DefaultTask) {
    description = "Generate all SBE stubs from provided schemas"
    dependsOn 'generateMaxRateSbeStubs', 'generateWorkloadSbeStubs'
}

sourceSets {
    main {
        java.srcDir generatedPath
    }
}

compileJava{
    dependsOn(generateSbeStubs)
}
