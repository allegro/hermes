plugins {
    id 'application'
    id 'java-library'
}

mainClassName = 'pl.allegro.tech.hermes.consumers.HermesConsumers'

configurations {
    sbeClasspath
}

dependencies {
    implementation project(':hermes-common')
    api project(':hermes-tracker')
    implementation project(':hermes-metrics')
    implementation project(':hermes-schema')

    api group: 'org.springframework.boot', name: 'spring-boot-starter', version: versions.spring
    api group: 'org.eclipse.jetty', name: 'jetty-alpn-java-client', version: versions.jetty
    api group: 'org.eclipse.jetty.http2', name: 'http2-http-client-transport', version: versions.jetty

    implementation group: 'org.jctools', name: 'jctools-core', version: '1.2'

    api group: 'javax.jms', name: 'javax.jms-api', version: '2.0'
    implementation group: 'joda-time', name: 'joda-time', version: '2.5'

    implementation(group: 'com.github.rholder', name: 'guava-retrying', version: '2.0.0') {
        exclude module: 'guava'
    }

    implementation group: 'org.agrona', name: 'agrona', version: '1.0.6'

    implementation(group: 'org.hornetq', name: 'hornetq-jms-client', version: '2.4.1.Final') {
        exclude module: 'hornetq-native'
    }

    api(group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '1.115.1')

    testImplementation project(':hermes-test-helper')
    testImplementation group: 'org.apache.curator', name: 'curator-test', version: versions.curator
    testImplementation group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    testImplementation project(':hermes-common')

    testImplementation(group: 'com.jayway.awaitility', name: 'awaitility', version: '1.6.1') {
        exclude group: 'com.jayway.jsonpath', module: 'json-path'
    }

    testImplementation group: 'tech.allegro.schema.json2avro', name: 'converter', version: versions.json2avro

    testImplementation group: 'org.spockframework', name: 'spock-core', version: versions.spock
    testImplementation group: 'org.spockframework', name: 'spock-junit4', version: versions.spock
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: versions.junit_jupiter

    sbeClasspath group: 'uk.co.real-logic', name: 'sbe-all', version: '1.12.8'
}

def generatedPath = "${buildDir}/generated/java/"

task generateMaxRateSbeStubs(type: JavaExec) {
    description = "Generate SBE stubs for max-rate"
    classpath configurations.sbeClasspath
    main = 'uk.co.real_logic.sbe.SbeTool'
    systemProperties(
            'sbe.output.dir': generatedPath,
            'sbe.xinclude.aware': 'true'
    )
    args = ['src/main/resources/sbe/max-rate.xml']
}

task generateWorkloadSbeStubs(type: JavaExec) {
    description = "Generate SBE stubs for workload"
    classpath configurations.sbeClasspath
    main = 'uk.co.real_logic.sbe.SbeTool'
    systemProperties(
            'sbe.output.dir': generatedPath,
            'sbe.xinclude.aware': 'true'
    )
    args = ['src/main/resources/sbe/workload.xml']
}

task generateSbeStubs(type: DefaultTask) {
    description = "Generate all SBE stubs from provided schemas"
    dependsOn 'generateMaxRateSbeStubs', 'generateWorkloadSbeStubs'
}

sourceSets {
    main {
        java.srcDir generatedPath
    }
}

compileJava{
    dependsOn(generateSbeStubs)
}
